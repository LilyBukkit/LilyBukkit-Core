import java.nio.file.Path
import java.nio.file.Paths

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'com.brambolt.gradle.patching' version '2022.05.01-7057'
}

abstract class DeletePatches extends DefaultTask {

    @Input
    String rmPath = "none"

    static def rm(Path path) {
        File file = new File(path.toString())
        if (file.isDirectory()) {
            for (File sf : file.listFiles()) {
                rm(sf.toPath())
            }
        }
        file.delete()
    }

    @TaskAction
    def delete() {
        rm(Paths.get(rmPath))
    }
}

tasks.register("deletePatches", DeletePatches) {
    rmPath = "${projectDir}/patches"
}

processPatches {
    content = "${projectDir}/src/orig/java"
    patches = "${projectDir}/patches"
    destination = "${projectDir}/src/main/java"
}

createPatches {
    content = "${projectDir}/src/orig/java"
    modified = "${projectDir}/src/main/java"
    destination = "${projectDir}/patches"
    dependsOn tasks.named("deletePatches").get()
}

jar {
    manifest {
        attributes "Main-Class": "net.minecraft.server.MinecraftServer"
    }
}

repositories{
    mavenCentral()
}

dependencies{
    implementation group: "org.yaml", name: "snakeyaml", version: "1.7"
    implementation group: "com.googlecode.json-simple", name: "json-simple", version: "1.1"
    implementation group: "org.avaje", name: "ebean", version: "2.7.3"
    implementation group: "com.google.guava", name: "guava-collections", version: "r03"
}

group 'net.buj'
version '1.1-SNAPSHOT'
